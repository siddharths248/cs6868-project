type 'a t = {
  announce : 'a Node.t Atomic.t array;
  head : 'a Node.t Atomic.t array;
  tail : 'a Node.t;
  num_threads : int;
}

let create num_threads = 
  let tail = Node.create (fun x -> x) num_threads in 
  Node.set_seq tail 1;
  {
    head = Array.init num_threads (fun _ -> Atomic.make tail);
    announce = Array.init num_threads (fun _ -> Atomic.make tail);
    tail;
    num_threads
  }

let apply wfu_obj invoc new_obj tid =
  let anc = Node.create invoc wfu_obj.num_threads in
  Atomic.set wfu_obj.announce.(tid) anc;
  Atomic.set wfu_obj.head.(tid) (Node.max (Array.map Atomic.get wfu_obj.head));
  let rec aux () = 
    let ann_i = Atomic.get wfu_obj.announce.(tid) in
    if Node.get_seq ann_i <> 0 then ()
    else begin
      let before = Atomic.get wfu_obj.head.(tid) in
      let idx = ((Node.get_seq before) + 1) mod wfu_obj.num_threads in
      let help =  Atomic.get wfu_obj.announce.(idx) in
      let prefer = begin
        if Node.get_seq help = 0 then help
        else Atomic.get wfu_obj.announce.(tid)
      end in
      let after = CASConsensus.decide (Node.get_decide_next before) prefer tid in
      Node.set_next before after;
      Node.set_seq after (Node.get_seq before + 1);
      Atomic.set wfu_obj.head.(tid) after;
      aux ()
    end
  in aux ();
  let ann_i = Atomic.get wfu_obj.announce.(tid) in
  Atomic.set wfu_obj.head.(tid) ann_i;
  let rec app current acc = 
    if current = Some ann_i then (Node.get_invoc ann_i) acc
    else begin match current with
    | Some node -> app (Node.get_next node) ((Node.get_invoc node) acc)
    | None -> failwith "This should never happen"
    end
  in
  app (Node.get_next wfu_obj.tail) new_obj